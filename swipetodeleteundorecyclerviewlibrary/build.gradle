apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.3"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "com.android.support:appcompat-v7:$appcompat_version"
    compile "com.android.support:support-v4:$appcompat_version"
    compile "com.android.support:recyclerview-v7:$appcompat_version"
    compile "com.android.support:support-annotations:$appcompat_version"

    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}


def artifact = new Properties()
artifact.load(new FileInputStream("deploy.settings"))

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
group = artifact.groupId
version = artifact.version

artifacts {
    archives sourcesJar
}
install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId artifact.groupId
            artifactId artifact.id
            version artifact.version
            name artifact.id // pom.project.name must be same as bintray.pkg.name
            url artifact.siteUrl
            inceptionYear '2017'
            licenses {
                license {
                    name 'Apache-2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0'
                    distribution 'repo'
                }
            }
            scm {
                connection artifact.gitUrl
                developerConnection artifact.gitUrl
                url artifact.siteUrl
            }
        }
    }
}

bintray {
    user = artifact.user
    key = artifact.apiKey
    configurations = ['archives']
    pkg {
        repo = 'swipeToDeleteUndoRecyclerViewLib'
        name = artifact.id
        userOrg = user
        publish = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/dadoz/RecyclerViewSwipeToDeleteUndo'
        version {
            name = artifact.version
            desc = 'Description bla bla bla'
            released  = new Date()
            vcsTag = artifact.version
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}
repositories {
    mavenCentral()
}
